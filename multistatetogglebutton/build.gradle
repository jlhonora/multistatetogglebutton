apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

def mVersionCode = 10
def mVersionName = "0.2.2"
group = "org.honorato.multistatetogglebutton"
version = mVersionName

def siteUrl = 'https://github.com/jlhonora/MultiStateToggleButton'
def gitUrl = 'https://github.com/jlhonora/MultiStateToggleButton.git'
def gitSSHUrl = 'git@github.com:jlhonora/MultiStateToggleButton.git'

android {
    compileSdkVersion 28
    buildToolsVersion "23.0.3"

    defaultConfig {
        minSdkVersion 23
        targetSdkVersion 28
        versionCode mVersionCode
        versionName mVersionName
    }

    buildTypes {
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            res.srcDirs = ['res']
        }
    }
}

Properties properties = new Properties()
File file = project.rootProject.file('local.properties')
if (file.isFile()) {
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
}

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        repo = "maven"
        name = "MultiStateToggleButton"
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        issueTrackerUrl = siteUrl + '/issues'
        licenses = ["MIT"]
        publish = true
        publicDownloadNumbers = true
    }
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'

                name 'A multi-state toggle button for Android'
                url siteUrl

                // Set your license
                licenses {
                    license {
                        name 'MIT License'
                        url 'http://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id 'jlhonora'
                        name 'Jose Luis Honorato'
                        email 'joseluishonorato@gmail.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

dependencies {
    implementation 'androidx.core:core:1.3.2'
    implementation 'androidx.annotation:annotation:1.1.0'
    implementation 'com.google.android.material:material:1.3.0'
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
//    classifier = 'sources'
    archiveClassifier.set("sources")
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
//    classifier = 'javadoc'
    archiveClassifier.set("javadoc")
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

task findConventions {
    doLast {
        println project.getConvention()
    }
}
